managed;//implementation in class zbp_i_travel_jaf unique;
strict ( 2 );

define behavior for ZI_Travel_jaf //alias <alias_name>
implementation in class zcl_bp_travel_jaf unique
persistent table ztravel_jaf
lock master
authorization master ( instance )
etag master LastChangedAt
early numbering
{
  create ( authorization : global );
  update;
  delete;
  field ( readonly ) TravelId, LastChangedBy, LastChangedAt, CreatedAt, CreatedBy;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;

  action ( features : instance ) accepttravel result [1] $self;
  action ( features : instance ) rejecttravel result [1] $self;
  factory action copytravel [1];
  internal action recalculate;
  validation validatecystomer on save { create; field CustomerId; }
  validation validatedate on save { create; field BeginDate, EndDate; }
  validation validatestatus on save { create; field OverallStatus; }
  validation validatecurrency on save { create; field CurrencyCode; }
  validation validatebookingfee on save { create; field BookingFee; }
  association _booking { create ( features : instance ); }
  mapping for ztravel_jaf
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }
}

define behavior for Zi_booking_jaf //alias <alias_name>
implementation in class zcl_bp_booking_jaf unique
persistent table zbooking_jaf
lock dependent by _travel
authorization dependent by _travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( mandatory : create, readonly : update ) BookingDate, CustomerId;

  association _travel;
  association _bookingsuppl { create ( features : instance ); }
  mapping for zbooking_jaf
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for zi_bookingsupp_jaf //alias <alias_name>
implementation in class zcl_bp_bookingsuppl_jaf unique
persistent table zbookingsupp_jaf
lock dependent by _travel
authorization dependent by _travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) Price, SupplementId;
  association _travel;
  association _booking;
  mapping for zbookingsupp_jaf
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}