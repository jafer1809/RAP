@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'booking interface view'
@Metadata.ignorePropagatedAnnotations: true
define view entity Zi_booking_jaf
  as select from zbooking_jaf
  association        to parent ZI_Travel_jaf     as _travel     on  $projection.TravelId = _travel.TravelId
  composition [0..*] of zi_bookingsupp_jaf       as _bookingsuppl
  association [1..1] to /DMO/I_Carrier           as _carrier    on  $projection.CarrierId = _carrier.AirlineID
  association [1..1] to /DMO/I_Customer          as _customer   on  $projection.CustomerId = _customer.CustomerID
  association [1..1] to /DMO/I_Connection        as _connection on  $projection.ConnectionId = _connection.ConnectionID
                                                                and $projection.CarrierId    = _connection.AirlineID
  association [1..1] to /DMO/I_Booking_Status_VH as _status     on  $projection.BookingStatus = _status.BookingStatus
{
  key travel_id       as TravelId,
  key booking_id      as BookingId,
      booking_date    as BookingDate,
      customer_id     as CustomerId,
      carrier_id      as CarrierId,
      connection_id   as ConnectionId,
      flight_date     as FlightDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      flight_price    as FlightPrice,
      currency_code   as CurrencyCode,
      booking_status  as BookingStatus,
      @Semantics.systemDateTime.localInstanceLastChangedAt: true
      last_changed_at as LastChangedAt,
      _carrier,
      _customer,
      _connection,
      _status,
      _travel,
      _bookingsuppl
}
